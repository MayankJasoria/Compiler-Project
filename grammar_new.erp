program			: moduleDeclarations otherModules driverModule otherModules
moduleDeclarations	: moduleDeclaration moduleDeclarations | EMPTY
moduleDeclaration	: DECLARE MODULE ID SEMICOL
otherModules		: module otherModules | EMPTY
driverModule		: DRIVERDEF DRIVER PROGRAM DRIVERENDDEF moduleDef
module		: DEF MODULE ID ENDDEF TAKES INPUT SQBO input_plist SQBC SEMICOL ret moduleDef
ret			: RETURNS SQBO output_plist SQBC SEMICOL | EMPTY
input_plist		: ID COLON dataType input_plistNew
input_plistNew	: COMMA ID COLON dataType input_plistNew | EMPTY
output_plist		: ID COLON type output_plistNew
output_plistNew	: COMMA ID COLON type output_plistNew | EMPTY
type			: INTEGER | REAL | BOOLEAN
dataType		: type | ARRAY SQBO range_arrays SQBC OF type
moduleDef		: START statements END
statements		: statement statements | EMPTY
statement		: ioStmt | simpleStmt | declareStmt | condionalStmt | iterativeStmt
ioStmt		: GET_VALUE BO ID BC SEMICOL | PRINT BO var BC SEMICOL
whichId		: SQBO index SQBC | EMPTY
index			: NUM | ID
simpleStmt		: assignmentStmt | moduleReuseStmt
assignmentStmt  : ID whichStmt
moduleReuseStmt	: optional USE MODULE ID WITH PARAMETERS idList SEMICOL
optional		: SQBO idList SQBC ASSIGNOP | EMPTY
idList		: ID idListNew
idListNew		: COMMA ID idListNew | EMPTY 
expression      : arithOrBoolExpr | u
arithOrBoolExpr 	: relopExpr arithOrBoolExprNew
arithOrBoolExprNew 	: logicalOp relopExpr arithOrBoolExprNew | EMPTY
relopExpr 		: arithmeticExpr relopExprNew
relopExprNew 		: relationalOp arithmeticExpr | EMPTY 
arithmeticExpr	: term arithmeticExprNew
arithmeticExprNew	: pm term arithmeticExprNew | EMPTY 
term		: factor termNew
termNew		: md factor termNew | EMPTY
factor		: BO arithmeticExpr BC | var_id_num
var			: var_id_num | boolConstt
pm			: PLUS | MINUS
md			: MUL | DIV
logicalOp		: AND | OR
relationalOp		: LT | LE | GT | GE | EQ | NE
declareStmt		: DECLARE idList COLON dataType SEMICOL
condionalStmt		: SWITCH BO ID BC START caseStmts default END 
caseStmts		: CASE value COLON statements BREAK SEMICOL caseStmtsNew
caseStmtsNew		: CASE value COLON statements BREAK SEMICOL caseStmtsNew | EMPTY
value			: NUM | TRUE | FALSE
default		: DEFAULT COLON statements BREAK SEMICOL | EMPTY
iterativeStmt		: FOR BO ID IN range BC START statements END | WHILE BO arithOrBoolExpr BC START statements END
range			: NUM RANGEOP NUM
range_arrays    : index RANGEOP index
boolConstt  : TRUE | FALSE
var_id_num  : ID whichId | NUM | RNUM
whichStmt       : lvalueIDStmt | lvalueARRStmt
lvalueIDStmt    : ASSIGNOP expression SEMICOL
lvalueARRStmt   : SQBO index SQBC ASSIGNOP expression SEMICOL
u               : unary_op new_NT 
unary_op        : PLUS | MINUS
new_NT          : BO arithmeticExpr BC | var_id_num
